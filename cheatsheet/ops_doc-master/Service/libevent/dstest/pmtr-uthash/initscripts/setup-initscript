#!/usr/bin/perl

##############################################################################
#
# This tool installs an initscript that runs pmtr as a system service.
# Since initscripts vary by platform, this script can attempt to figure out
# the initsystem (if unspecified), or it can be told explicitly (systemd,
# upstart, etc to use). To see the supported options run:
#
# ./setup-initscript --help
#
# a typical usage looks like:
#
# sudo ./setup-initscript --initsys systemd --install-service --start-service
#
##############################################################################

use strict;
use warnings;
use FindBin qw($RealBin);
use Getopt::Long;
use File::Basename;

sub usage {
  print STDERR "usage: $0 [options]\n";
  print STDERR "options: --quiet\n";
  print STDERR "         --auto\n";
  print STDERR "         --dry-run\n";
  print STDERR "         --install-service <auto|path-to-file>\n";
  print STDERR "         --enable-service\n";
  print STDERR "         --start-service\n";
  print STDERR "         --initsys <auto|systemd|sysvinit|debian|upstart|rhel6>\n";
  print STDERR "         --bindir <auto|dir> (e.g. /usr/bin)\n";
  print STDERR "         --no-color\n";
  exit -1;
}

our $auto = 0;
our $dry_run = 0;
our $start = 0;
our $enable = 0;
our $stop = 0;
our $install;
our $uninstall = 0;
our $verbose = 0;
our $quiet = 0;
our $initsys = "auto";
our $bindir = "auto";
our $help = 0;
our $nocolor = 0;
our $bold="";
our $normal="";
our $green="";

my %cfg;
$cfg{systemd}{enable} = "systemctl enable pmtr";
$cfg{systemd}{disable} = "systemctl disable pmtr";
$cfg{systemd}{start}  = "systemctl start pmtr";
$cfg{systemd}{stop}  = "systemctl stop pmtr";
$cfg{systemd}{service}= "/etc/systemd/system/pmtr.service";
$cfg{systemd}{mode}=    0644;

$cfg{rhel6}{enable}   = "/sbin/chkconfig --add pmtr";
$cfg{rhel6}{disable}   = "/sbin/chkconfig pmtr off";
$cfg{rhel6}{start}    = "/etc/init.d/pmtr start";
$cfg{rhel6}{stop}    = "/etc/init.d/pmtr stop";
$cfg{rhel6}{service}  = "/etc/rc.d/init.d/pmtr";
$cfg{rhel6}{mode}=    0755;

$cfg{upstart}{enable} = "/bin/true";
$cfg{upstart}{disable} = "/bin/true";
$cfg{upstart}{start}  = "/sbin/start pmtr";
$cfg{upstart}{stop}  = "/sbin/stop pmtr";
$cfg{upstart}{service}= "/etc/init/pmtr.conf";
$cfg{upstart}{mode}=    0644;

$cfg{sysvinit}{enable}  = "/usr/sbin/update-rc.d pmtr defaults";
$cfg{sysvinit}{disable}  = "/usr/sbin/update-rc.d -f pmtr remove";
$cfg{sysvinit}{start}   = "/etc/init.d/pmtr start";
$cfg{sysvinit}{stop}   = "/etc/init.d/pmtr stop";
$cfg{sysvinit}{service} = "/etc/init.d/pmtr";
$cfg{sysvinit}{mode}=    0755;

$cfg{debian}{enable}  = "/usr/sbin/update-rc.d pmtr defaults";
$cfg{debian}{disable}  = "/usr/sbin/update-rc.d -f pmtr remove";
$cfg{debian}{start}   = "/usr/sbin/service pmtr start";
$cfg{debian}{stop}   = "/usr/sbin/service pmtr stop";
$cfg{debian}{service} = "/etc/init.d/pmtr";
$cfg{debian}{mode}=    0755;

usage unless GetOptions( "quiet"             => \$quiet,
                         "verbose+"          => \$verbose,
                         "auto"              => \$auto,
                         "dry-run"           => \$dry_run,
                         "bindir=s"          => \$bindir,
                         "initsys=s"         => \$initsys,
                         "start-service"     => \$start,
                         "enable-service"    => \$enable,
                         "stop-service"      => \$stop,
                         "install-service=s" => \$install,
                         "uninstall-service" => \$uninstall,
                         "no-color"          => \$nocolor,
                         "help"              => \$help);
usage if $help;
# auto is a shortcut for --install-service auto --enable-service --start-service
if ($auto) {
  $install="auto";
  $enable=1;
  $start=1;
}
usage unless ($install or $start or $stop or $uninstall);

#die "Please run as root. Exiting.\n" unless ($dry_run or ($> == 0));

sub check_colors {
   `tput colors 2>/dev/null`;
   $nocolor=1 if $?;
   return if $nocolor;
	
   $bold=`tput bold`;
   $normal=`tput sgr0`;
   $green=`tput setaf 2`;
}

# bold/colorize input string if colors enabled
sub emit {
  my $label = shift;
  my $in = shift;
  printf("%-20s: %s\n", $label, "${bold}${green}${in}${normal}") unless $quiet;
}

sub guess_bindir {
  my @dirs = qw(/bin /usr/bin /sbin /usr/sbin /usr/local/bin);
  for my $dir (@dirs) {
    if (-x "$dir/pmtr") {
      $bindir = $dir;
      return;
    }
  }
  die "can't locate pmtr executable: try using --bindir\n";
}

sub locate_bindir {
  guess_bindir if $bindir eq "auto";
  emit("pmtr", "${bindir}/pmtr");
}

sub guess_initsystem {
  my $target = readlink "/sbin/init";

  if ((-l "/sbin/init") and ($target =~ /systemd/)) { $initsys = "systemd"; }
  elsif (-d "/usr/lib/upstart")                     { $initsys = "upstart"; }
  elsif (-f "/etc/redhat-release")                  { $initsys = "rhel6";   }
  elsif (-f "/etc/debian_version")                  { $initsys = "debian";  }
  # Amazon Linux AMI (2016.09) uses rhel6 init
  elsif ((-f "/etc/system-release") and 
         (-x "/sbin/chkconfig"))                    { $initsys = "rhel6";}
  elsif (-d "/etc/init.d")                          { $initsys = "sysvinit";}
  else { die "can't guess initsystem"; }
  emit("initsystem", ${initsys});
}

sub install_service {
  my $cmd;
  local $/; # slurp mode

  my $path = $install;
  $path = $cfg{$initsys}{service} if ($path eq "auto");

  if ($dry_run) {
    print STDERR "dry-run: write $path\n";
    return;
  }

  # we need to replace __SYSBINDIR__ references in the initscript
  # template with the actual value (e.g. /usr/bin) provided to us.
  # The templates are in the same directory with this script. Since
  # we may be invoked from a third directory, we need to use $RealBin
  # to find the directory where this script is running from.
  my $template = "${RealBin}/${initsys}.template";
  open TEMPLATE, "<$template" or die "can't open $template: $!";
  my $script = <TEMPLATE>;
  $script =~ s/__SYSBINDIR__/$bindir/g;
  close TEMPLATE;

  my $dir = dirname($path);
  $cmd = "install -d $dir";
  emit("services", $dir);
  die "error creating $dir: $!" if system $cmd;

  open SCRIPT, ">$path" or die "can't open $path: $!\n";
  print SCRIPT $script;
  close SCRIPT;
  chmod $cfg{$initsys}{mode}, $path;
  emit("initscript", $path);

}

sub enable_service {
  my $cmd;

  if ($dry_run) {
    print STDERR "dry-run: run $cfg{$initsys}{enable}\n";
    return;
  }

  # enable the new service
  $cmd = $cfg{$initsys}{enable};
  emit("enabling", $cmd);
  die "error enabling iniscript" if system $cmd;
}

sub uninstall_service {
  my $cmd;
  local $/; # slurp mode

  if ($dry_run) {
    print STDERR "dry-run: run $cfg{$initsys}{disable}\n";
    print STDERR "dry-run: delete $cfg{$initsys}{service}\n";
    return;
  }

  # disable the service
  $cmd = $cfg{$initsys}{disable};
  emit("disabling", $cmd);
  die "error disabling iniscript" if system $cmd;

  # remove its script
  emit("unlinking", $cfg{$initsys}{service});
  unlink $cfg{$initsys}{service} or 
     die "can't unlink $cfg{$initsys}{service}: $!\n";

}

sub start_service {
  my $cmd = $cfg{$initsys}{start};
  if ($dry_run) {
    print STDERR "dry-run: run $cmd\n";
    return;
  }
  emit("starting service", $cmd);
  die "error starting service" if system $cmd;
}

sub stop_service {
  my $cmd = $cfg{$initsys}{stop};
  if ($dry_run) {
    print STDERR "dry-run: run $cmd\n";
    return;
  }
  emit("stopping service", $cmd);
  die "error stopping service" if system $cmd;
}

check_colors unless $nocolor;
guess_initsystem if $initsys eq "auto";
locate_bindir;
stop_service if $stop;
install_service if $install;
enable_service if $enable;
uninstall_service if $uninstall;
start_service if $start;

