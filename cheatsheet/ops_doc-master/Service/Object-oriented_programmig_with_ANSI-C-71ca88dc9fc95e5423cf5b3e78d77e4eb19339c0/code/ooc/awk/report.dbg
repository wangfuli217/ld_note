#	report.dbg -- 5.3 Jun 13 14:25:42 1993
#	Copyright (c) 1993 Axel T. Schreiner

# report generator debugging

#	dumpReport(r)			dump report by name

$1 ~ /\.rep$/ {				# filename.rep
	loadReports($1)
}

$1 != "" && $1 in Report {		# reportname
	dumpReport($1); next
}

$1 == "all" || $1 == "reports" {	# all | reports
	for (r in Report)
		if (r != "")
			dumpReport(r)
}

END {
	exit status
}

function dumpReport (r,    i, brace) {
	if (! (r in Report))
		fatal(r " ?cannot find report")
	print "report " r ":"
	brace = 0
	for (i = Report[r];; ++ i)
		if (Token[i] ~ /^`{/)
		{	printf "%s >>%d ", Token[i], Token[i+1]
			++ i; ++ brace
		}
		else if (Token[i] ~ /^`}/)
			if (-- brace < 0)
				break
			else
				printf "%s [%d] ", Token[i], i+1
		else if (Token[i] == "\n")
			printf "\n"
		else
			printf "%s ", Token[i]
	printf "\n"
}
